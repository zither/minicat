apply plugin: 'com.android.application'

dependencies {
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'

    implementation('com.google.code.gson:gson:2.6.2')

    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'
    implementation 'de.hdodenhof:circleimageview:2.0.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation('com.github.ksoichiro:android-observablescrollview:1.6.0') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    implementation('com.nostra13.universalimageloader:universal-image-loader:1.9.1')
    implementation('com.nineoldandroids:library:2.4.0')
    implementation('com.jakewharton:butterknife:6.1.0')
    implementation('com.squareup.picasso:picasso:2.5.2')

    implementation('com.github.castorflex.smoothprogressbar:library:1.1.0')
    implementation('com.mcxiaoke.viewpagerindicator:library:2.4.1') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    implementation('com.mcxiaoke.oauthsimple:oauthsimple:2.0.1') {
        exclude group: 'org.json', module: 'json'
    }
    implementation('com.mcxiaoke.commons:library:1.0.2') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    implementation 'com.mcxiaoke.photoview:library:1.2.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.4'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.mcxiaoke.xbus:bus:1.0.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionName "1.5.6"
        versionCode 201901251
        applicationId "com.mcxiaoke.minicat2"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    signingConfigs {
        release {
//            storeFile file(project.ANDROID_KEY_STORE)
//            storePassword project.ANDROID_STORE_PASSWORD
//            keyAlias project.ANDROID_KEY_ALIAS
//            keyPassword project.ANDROID_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
        }

        beta {
            signingConfig signingConfigs.release
            versionNameSuffix "-beta"
            debuggable true
        }

        debug {
            applicationIdSuffix null
            versionNameSuffix "-dev" + getDate()
            debuggable true
        }
    }

    lintOptions {
        quiet true
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }

}

checkSigningConfig()

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def checkSigningConfig() {
    if (project.hasProperty("ANDROID_KEY_STORE")
            && project.hasProperty("ANDROID_STORE_PASSWORD")
            && project.hasProperty("ANDROID_KEY_ALIAS")
            && project.hasProperty("ANDROID_KEY_PASSWORD")) {
        project.android.signingConfigs.release {
            storeFile file(project.ANDROID_KEY_STORE)
            storePassword project.ANDROID_STORE_PASSWORD
            keyAlias project.ANDROID_KEY_ALIAS
            keyPassword project.ANDROID_KEY_PASSWORD
        }
    }
}



